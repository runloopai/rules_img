"""Multi deploy rule for deploying multiple operations as a unified command."""

load("@bazel_skylib//rules:common_settings.bzl", "BuildSettingInfo")
load("@hermetic_launcher//launcher:lib.bzl", "launcher")
load("//img/private:root_symlinks.bzl", "calculate_root_symlinks", "symlink_name_prefix")
load("//img/private/common:build.bzl", "TOOLCHAIN", "TOOLCHAINS")
load("//img/private/common:default_deploy_tool.bzl", "default_deploy_tool")
load("//img/private/common:transitions.bzl", "reset_platform_transition")
load("//img/private/providers:deploy_info.bzl", "DeployInfo")
load("//img/private/providers:deploy_tool_info.bzl", "DeployToolInfo")
load("//img/private/providers:index_info.bzl", "ImageIndexInfo")
load("//img/private/providers:load_settings_info.bzl", "LoadSettingsInfo")
load("//img/private/providers:manifest_info.bzl", "ImageManifestInfo")
load("//img/private/providers:push_settings_info.bzl", "PushSettingsInfo")
load("//img/private/providers:stamp_setting_info.bzl", "StampSettingInfo")

def _multi_deploy_strategy(ctx, operation_type):
    """Determine the strategy to use based on the settings and operation type."""
    if operation_type == "push":
        push_settings = ctx.attr._push_settings[PushSettingsInfo]
        strategy = ctx.attr.push_strategy
        if strategy == "auto":
            strategy = push_settings.strategy
        return strategy
    elif operation_type == "load":
        load_settings = ctx.attr._load_settings[LoadSettingsInfo]
        strategy = ctx.attr.load_strategy
        if strategy == "auto":
            strategy = load_settings.strategy
        return strategy
    else:
        fail("Unknown operation type: {}".format(operation_type))

def _compute_multi_deploy_metadata(*, ctx):
    """Compute the merged deploy metadata from all operations."""
    inputs = []
    deploy_manifests = []
    layer_hints_files = []

    # Collect all deploy manifests and layer hints from operations
    for operation in ctx.attr.operations:
        deploy_info = operation[DeployInfo]
        deploy_manifests.append(deploy_info.deploy_manifest)
        inputs.append(deploy_info.deploy_manifest)
        if deploy_info.layer_hints != None:
            layer_hints_files.append(deploy_info.layer_hints)
            inputs.append(deploy_info.layer_hints)

    # Create the merge command
    merge_args = ctx.actions.args()
    merge_args.add("--push-strategy", _multi_deploy_strategy(ctx, "push"))
    merge_args.add("--load-strategy", _multi_deploy_strategy(ctx, "load"))

    # Add layer hints inputs and output if any exist
    layer_hints_out = None
    if layer_hints_files:
        merge_args.add_all(layer_hints_files, before_each = "--layer-hints-input")
        layer_hints_out = ctx.actions.declare_file(ctx.label.name + ".layer_hints")
        merge_args.add("--layer-hints-output", layer_hints_out)

    # Add input deploy manifest files
    merge_args.add_all(deploy_manifests)

    # Output file
    metadata_out = ctx.actions.declare_file(ctx.label.name + ".json")
    merge_args.add(metadata_out)
    merge_args.set_param_file_format("multiline")
    merge_args.use_param_file("@%s", use_always = True)

    outputs = [metadata_out]
    if layer_hints_out != None:
        outputs.append(layer_hints_out)

    img_toolchain_info = ctx.toolchains[TOOLCHAIN].imgtoolchaininfo
    ctx.actions.run(
        inputs = inputs,
        outputs = outputs,
        executable = img_toolchain_info.tool_exe,
        arguments = ["deploy-merge", merge_args],
        mnemonic = "MultiDeployMerge",
    )
    return metadata_out, layer_hints_out

def _collect_all_image_providers(ctx):
    """Collect all image providers from operations to build root symlinks."""
    images = []
    for operation in ctx.attr.operations:
        deploy_info = operation[DeployInfo]
        if hasattr(deploy_info.image, "manifests"):
            # It's an index
            images.append(dict(
                index_info = deploy_info.image,
                manifest_info = None,
            ))
        else:
            # It's a manifest
            images.append(dict(
                index_info = None,
                manifest_info = deploy_info.image,
            ))
    return images

def _multi_deploy_impl(ctx):
    """Implementation of the multi_deploy rule."""
    if not ctx.attr.operations:
        fail("operations attribute cannot be empty")

    for operation in ctx.attr.operations:
        if DeployInfo not in operation:
            if ImageManifestInfo in operation or ImageIndexInfo in operation:
                fail("Target '{}' provides an image but not DeployInfo. Add 'push_specs' or 'load_specs' to produce DeployInfo, or wrap it with image_push.".format(operation.label))
            else:
                fail("Target '{}' does not provide DeployInfo.".format(operation.label))

    # Merge all deploy manifests
    deploy_metadata, layer_hints = _compute_multi_deploy_metadata(ctx = ctx)

    # Create the executable
    root_symlinks_prefix = symlink_name_prefix(ctx)
    deployer = ctx.actions.declare_file(ctx.label.name + ".exe")
    deploy_tool_info = ctx.attr.deploy_tool[DeployToolInfo] if ctx.attr.deploy_tool != None else ctx.attr._deploy_tool[DeployToolInfo]
    embedded_args, transformed_args = launcher.args_from_entrypoint(executable_file = deploy_tool_info.img_deploy_exe)
    embedded_args.extend(["deploy", "--runfiles-root-symlinks-prefix", root_symlinks_prefix, "--request-file"])
    embedded_args, transformed_args = launcher.append_runfile(
        file = deploy_metadata,
        embedded_args = embedded_args,
        transformed_args = transformed_args,
    )
    launcher.compile_stub(
        ctx = ctx,
        embedded_args = embedded_args,
        transformed_args = transformed_args,
        output_file = deployer,
        template_file = deploy_tool_info.launcher_template,
    )

    # Collect all image providers for root symlinks
    images = _collect_all_image_providers(ctx)

    root_symlinks = {}

    # Add symlinks for all deploy commands, using per-operation include_layers from DeployInfo
    for (i, image) in enumerate(images):
        symlinks = calculate_root_symlinks(
            index_info = image["index_info"],
            manifest_info = image["manifest_info"],
            include_layers = ctx.attr.operations[i][DeployInfo].include_layers,
            operation_index = i,
            symlink_name_prefix = root_symlinks_prefix,
        )
        root_symlinks.update(symlinks)

    # Add merged layer hints to root symlinks if present
    if layer_hints != None:
        root_symlinks["{}layer_hints".format(root_symlinks_prefix)] = layer_hints

    # Merge environment settings from push and load
    environment = {}
    inherited_environment = ["DOCKER_CONFIG", "IMG_AUTH_DEBUG"]

    push_settings = ctx.attr._push_settings[PushSettingsInfo]
    load_settings = ctx.attr._load_settings[LoadSettingsInfo]

    if push_settings.remote_cache or load_settings.remote_cache:
        environment["IMG_REAPI_ENDPOINT"] = push_settings.remote_cache or load_settings.remote_cache
        inherited_environment.append("IMG_REAPI_ENDPOINT")

    if push_settings.remote_instance_name or load_settings.remote_instance_name:
        environment["IMG_REAPI_INSTANCE_NAME"] = push_settings.remote_instance_name or load_settings.remote_instance_name
        inherited_environment.append("IMG_REAPI_INSTANCE_NAME")

    if push_settings.credential_helper or load_settings.credential_helper:
        environment["IMG_CREDENTIAL_HELPER"] = push_settings.credential_helper or load_settings.credential_helper
        inherited_environment.append("IMG_CREDENTIAL_HELPER")

    # Add REGISTRY_AUTH_FILE if docker_config_path is set
    docker_config_path = ctx.attr._docker_config_path[BuildSettingInfo].value
    if docker_config_path:
        environment["REGISTRY_AUTH_FILE"] = docker_config_path

    environment.update(ctx.attr.env)

    return [
        DefaultInfo(
            files = depset([deployer]),
            executable = deployer,
            runfiles = ctx.runfiles(
                files = [
                    deploy_tool_info.img_deploy_exe,
                    deploy_metadata,
                ],
                root_symlinks = root_symlinks,
            ),
        ),
        OutputGroupInfo(
            deploy_manifest = depset([deploy_metadata]),
        ),
        RunEnvironmentInfo(
            environment = environment,
            inherited_environment = inherited_environment,
        ),
    ]

multi_deploy = rule(
    implementation = _multi_deploy_impl,
    doc = """Deploys multiple container images in a single coordinated command.

Use `push_specs` and `load_specs` on your image targets to attach deployment
configuration directly, then reference the images in `operations`:

```python
load("@rules_img//img:image.bzl", "image_manifest")
load("@rules_img//img:push.bzl", "image_push_spec")
load("@rules_img//img:multi_deploy.bzl", "multi_deploy")

image_push_spec(
    name = "push_spec",
    registry = "gcr.io",
    repository = "my-project/{{.image_target_name}}",
    tag = "latest",
)

image_manifest(
    name = "frontend",
    base = "@distroless_cc",
    layers = [":frontend_layer"],
    push_specs = [":push_spec"],
)

image_manifest(
    name = "backend",
    base = "@distroless_cc",
    layers = [":backend_layer"],
    push_specs = [":push_spec"],
)

multi_deploy(
    name = "deploy_all",
    operations = [
        ":frontend",
        ":backend",
    ],
)
```

Alternatively, standalone `image_push` or `image_load` targets that already
provide `DeployInfo` can be used directly in `operations`.

Runtime usage:
```bash
bazel run //path/to:deploy_all
```
""",
    attrs = {
        "operations": attr.label_list(
            doc = """List of operations to deploy together.

Each operation must provide DeployInfo (typically from image_push, image_load,
image_manifest with push_specs/load_specs, or image_index with push_specs/load_specs).
All operations will be merged and executed in the order specified.
""",
            mandatory = True,
            # OR-semantics: accepts image targets without DeployInfo so that
            # _multi_deploy_impl can provide actionable error messages guiding
            # users to add push_specs/load_specs or wrap with image_push.
            providers = [[DeployInfo], [ImageManifestInfo], [ImageIndexInfo]],
        ),
        "push_strategy": attr.string(
            doc = """Push strategy to use for all push operations in the deployment.

See [push strategies documentation](/docs/push-strategies.md) for detailed information.
""",
            default = "auto",
            values = ["auto", "eager", "lazy", "cas_registry", "bes"],
        ),
        "load_strategy": attr.string(
            doc = """Load strategy to use for all load operations in the deployment.

Available strategies:
- **`auto`** (default): Uses the global default load strategy
- **`eager`**: Downloads all layers during the build phase
- **`lazy`**: Downloads layers only when needed during the load operation
""",
            default = "auto",
            values = ["auto", "eager", "lazy"],
        ),
        "env": attr.string_dict(
            doc = """Environment variables to set when running the deployer and credential helpers.

Example:
```python
env = {
    "AWS_PROFILE": "production",
    "DOCKER_HOST": "unix:///var/run/docker.sock",
}
```
""",
        ),
        "_push_settings": attr.label(
            default = Label("//img/private/settings:push"),
            providers = [PushSettingsInfo],
        ),
        "_load_settings": attr.label(
            default = Label("//img/private/settings:load"),
            providers = [LoadSettingsInfo],
        ),
        "_stamp_settings": attr.label(
            default = Label("//img/private/settings:stamp"),
            providers = [StampSettingInfo],
        ),
        "_docker_config_path": attr.label(
            default = Label("//img/settings:docker_config_path"),
            providers = [BuildSettingInfo],
        ),
        "tool_cfg": attr.string(
            doc = """Configuration of the deployer executable platform.

Available options:
- **`host`** (default): Deployer executable matches the host platform.
- **`target`**: Deployer executable matches the target platform(s) specified via `--platforms`.
""",
            default = "host",
            values = ["host", "target"],
        ),
        "deploy_tool": attr.label(
            doc = """Optional label of a deploy tool target providing `DeployToolInfo` (created with `img_deploy_tool` from `@rules_img//img:deploy_tool.bzl`). When set, overrides `tool_cfg`.""",
            providers = [DeployToolInfo],
        ),
        "_deploy_tool": attr.label(
            default = default_deploy_tool,
            providers = [DeployToolInfo],
        ),
    },
    executable = True,
    cfg = reset_platform_transition,
    toolchains = [
        launcher.finalizer_toolchain_type,
    ] + TOOLCHAINS,
)
